import org.apache.tools.ant.taskdefs.condition.Os

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 24
        compileSdkVersion = 34
        targetSdkVersion = 34
        supportLibVersion = "34.0.0"
        kotlinVersion = "1.8.0"
        ndkVersion = "25.1.8937393"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle"
        classpath "com.facebook.react:react-native-gradle-plugin"
        classpath "com.google.gms:google-services:4.3.15"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        jcenter() {
            content {
                includeGroup("com.google.android.exoplayer")
            }
        }
        maven {
            url 'https://sdk.withpersona.com/android/releases'
        }
    }

    // Needed to avoid the following error
    // `Inconsistent JVM-target compatibility detected for tasks 'compileDebugJavaWithJavac' (11) and 'compileDebugKotlin' (17).`
    // For devs running Java > 11
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile.class) {
        kotlinOptions {
            jvmTarget = "11"
        }
    }
}

subprojects {
  { project ->
    if (project.name.contains('react-native-firebase')) {
      buildscript {
        dependencies {
          classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41"
        }
      }
    }
  }

  afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}

apply plugin: "com.facebook.react.rootproject"
